// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext {
        gradle_version = '4.0.1'
        ktlint_version = '8.2.0'
        jetifier_version = '1.0.0-beta10'
        fabric_version = '1.31.2'
        mannodermaus_junit5_version = '1.6.0.0'

        // kotlin
        kotlin_version = '1.3.72'
        coroutines_version = '1.4.1'

        // unit test
        junit_version = '4.13.1'
        junit_jupiter_version = '5.7.0'
        mockk_version = '1.10.2'
        mockito_version = '3.5.13'
        androidx_core_testing_version = '2.1.0'

        // androidx common
        androidx_core_ktx_version = '1.3.2'
        androidx_appcompat_version = '1.2.0'
        androidx_annotation_version = '1.1.0'
        androidx_multidex_version = '2.0.1'
        androidx_constraint_layout_version = '2.0.4'
        androidx_recyclerview_version = '1.2.0-alpha06'

        // androidx lifecycle
        androidx_lifecycle_version = '2.2.0'

        // androidx room
        androidx_room_version = '2.3.0-alpha03'

        // androidx paging
        androidx_paging_version = '3.0.0-alpha09'

        // dagger - dependency injection
        dagger_version = '2.29.1'

        // material support
        design_support_version = '1.3.0-alpha03'

        // rest api connection
        retrofit2_version = "2.9.0"
        okhttp3_version = '4.9.0'

        // image loader
        glide_version = "4.11.0"

        // log printer
        timber_version = '4.7.1'
        logger_version = '2.2.0'
    }

    repositories {
        google()
        jcenter()
        mavenCentral()
        maven { url "https://jitpack.io" }
        maven { url 'https://oss.jfrog.org/artifactory/oss-snapshot-local' }
        maven { url 'https://maven.fabric.io/public' }
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$gradle_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version"
        classpath "org.jlleitschuh.gradle:ktlint-gradle:$ktlint_version"
        classpath "com.android.tools.build.jetifier:jetifier-processor:$jetifier_version"
        classpath "io.fabric.tools:gradle:$fabric_version"
        classpath "de.mannodermaus.gradle.plugins:android-junit5:$mannodermaus_junit5_version"
    }
}

plugins {
    id "com.diffplug.gradle.spotless" version "$gradle_version"
    id "org.jlleitschuh.gradle.ktlint" version "$ktlint_version"
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url 'https://maven.google.com' }
        flatDir { dirs '../../' }
        maven { url 'https://jitpack.io' }
        maven {
            name 'glide-snapshot'
            url 'http://oss.sonatype.org/content/repositories/snapshots'
        }
    }

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xmaxerrs" << "1000"
        }
    }
}

spotless {
    ktlint {
        debug.set(true)
    }
}

spotless {
    java {
        target '**/*.java'
        googleJavaFormat().aosp()
        removeUnusedImports()
        trimTrailingWhitespace()
        indentWithSpaces()
        endWithNewline()
    }

    kotlin {
        target "**/*.kt"
        ktlint(ktlint_version)
        trimTrailingWhitespace()
        indentWithSpaces()
        endWithNewline()
    }

    format 'misc', {
        target '**/*.gradle', '**/*.md', '**/.gitignore'
        indentWithSpaces()
        trimTrailingWhitespace()
        endWithNewline()
    }

    format 'xml', {
        target '**/*.xml'
        indentWithSpaces()
        trimTrailingWhitespace()
        endWithNewline()
    }
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        def requested = details.requested
        if (requested.group == "com.android.support") {
            if (!requested.name.startsWith("multidex")) {
                details.useVersion "26.+"
            }
        }

        if (requested.group == "androidx.appcompat") {
            if (!requested.name.startsWith("multidex")) {
                details.useVersion "1.+"
            }
        }
    }

    exclude group: 'com.google.protobuf', module: 'protobuf-lite'
    exclude group: 'com.google.protobuf', module: 'protobuf-java'

    exclude group: "com.android.support", module: "support-core-ui"
    exclude group: "com.android.support", module: "support-compat"
}